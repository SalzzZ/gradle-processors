plugins {
  id 'java-gradle-plugin'
  id 'groovy'
  id 'com.gradle.plugin-publish' version '0.9.1'
}

repositories {
  mavenCentral()
}

dependencies {
  compile localGroovy()
  compile 'org.codehaus.groovy:groovy-backports-compat23:2.3.5'
  testCompile gradleTestKit()
}

group = 'org.inferred'
version = '1.1.1'

pluginBundle {
  website = 'https://github.com/palantir/gradle-processors'
  vcsUrl = 'https://github.com/palantir/gradle-processors'

  plugins {
    greetingsPlugin {
      displayName = 'Processors plugin'
      id = 'org.inferred.processors'
      description = 'Clean integration of Java 6+ annotation processors with the Eclipse and IDEA plugins'
      tags = ['java', 'annotation', 'processor']
    }
  }
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
  def outputDir = file("$buildDir/$name")

  inputs.files sourceSets.main.runtimeClasspath
  outputs.dir outputDir

  doLast {
    outputDir.mkdirs()
    file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
  }
}

// Add the classpath file to the test runtime classpath
dependencies {
  testRuntime files(createClasspathManifest)
}
