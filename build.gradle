plugins {
  id 'java-gradle-plugin'
  id 'groovy'
  id 'maven-publish'
  id 'nebula.release' version '6.0.0'
}

repositories {
  jcenter()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
  compile localGroovy()
  compile 'org.codehaus.groovy:groovy-backports-compat23:2.3.5'
}

//// Tests ////////////////////////////////////////////////////////////////////////////

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
  def outputDir = file("$buildDir/$name")

  inputs.files sourceSets.main.runtimeClasspath
  outputs.dir outputDir

  doLast {
    outputDir.mkdirs()
    file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
  }
}

// Add the classpath file to the test runtime classpath
dependencies {
  testCompile gradleTestKit()
  testCompile "junit:junit:4.12"
  testRuntime files(createClasspathManifest)
}

test {
  testLogging {
    exceptionFormat = 'full'
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      url "${maven_url}"
      credentials {
        username = "${maven_user}"
        password = "${maven_password}"
      }
    }
  }
}

tasks.getByPath("final").finalizedBy(tasks.getByPath("publish"))
tasks.getByPath("candidate").finalizedBy(tasks.getByPath("publishToMavenLocal"))
